//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import '@openzeppelin/contracts/utils/Address.sol';
import '@openzeppelin/contracts/utils/math/SafeMath.sol';
import "@openzeppelin/contracts/interfaces/IERC721.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

// @title The SelfCrypto contract is used to provide decentralized key management services.
// @author refitor
contract SelfCrypto is Ownable {
    using ECDSA for bytes32;
    using SafeMath for uint256;
    using Address for address payable;
    struct MetaData {
        address wallet;
        address nftAddr;
        uint256 storeFee;
        uint256 feeRate;
        uint256 registTotal;
    }
    struct StoreData {
        bytes recoverID;
        bytes web3Key;
        bytes backendKey;
    }
    MetaData private _metaData;
    mapping (address => StoreData) private _dataMap;

    /**
     * @dev Reset is used to populate the meta information of the contract.
     * @param wallet the wallet address specified by the creator.
     * @param nftAddr the NFT address specified by the creator.
     * @param storeFee one-time storage fee for user registration.
     * @param feeRate fee deduction percentage set by the contract creator, demo: 500 / 10000, feeRate is 500.
     */
    function Reset(address wallet, address nftAddr, uint256 storeFee, uint256 feeRate) external onlyOwner {
        _set(wallet, nftAddr, storeFee, feeRate);
    }

    /**
     * @dev Register is used to register the web3 user.
     * @param recoverID recovery ID specified by the user.
     * @param web3Key web3 key specified by the front end.
     * @param backendKey backend key specified by the back end.
     */
    function Register(bytes calldata recoverID, bytes calldata web3Key, bytes calldata backendKey) external payable {
        StoreData memory sd = _getKV(msg.sender);
        require(sd.web3Key.length == 0, "duplicate registration");
        require(web3Key.length != 0, "web3Key must be non-empty");
        require(recoverID.length != 0, "recoverID must be non-empty");
        require(backendKey.length != 0, "backendKey must be non-empty");
        MetaData memory md = _get();
        if (md.nftAddr != address(0)) {
            require(IERC721(md.nftAddr).balanceOf(msg.sender) != 0, "illegal nft holder");
        } else if (md.storeFee > 0 && md.feeRate > 0) {
            require(msg.value > 0, "store fee must be greater than 0");
            require(msg.value == md.storeFee, "invalid registration fee");
            require(msg.value * md.feeRate / 10000 > 0, "invalid registration fee");
            payable(md.wallet).sendValue(msg.value * md.feeRate / 10000);
        }
        sd = StoreData(recoverID, web3Key, backendKey);
        _setKV(msg.sender, sd);
    }

    /**
     * @dev Load is used to load the key information.
     * @param signature signature signed by the web3 user.
     * @param message random string dynamically generated by the front end.
     */
    function Load(bytes calldata signature, bytes calldata message) view external returns (bytes memory recoverID, bytes memory web3Key, bytes memory backendKey) {
        require(_verify(signature, message) == msg.sender, "permission denied");
        StoreData memory sd = _getKV(msg.sender);
        require(sd.web3Key.length != 0, "not registered yet");
        return (sd.recoverID, sd.web3Key, sd.backendKey);
    }

    /**
     * @dev Meta is used to load the meta information of the contract.
     */
    function Meta() view external returns (uint256 storeFee, address nftAddr, uint256 registTotal) {
        MetaData memory md = _get();
        return (md.storeFee, md.nftAddr, md.registTotal);
    }

    /**
     * @dev _verify is used to verify the signature.
     * @param signature signature signed by the web3 user.
     * @param message random string dynamically generated by the front end.
     */
    function _verify(bytes calldata signature, bytes calldata message) private pure returns (address) {
        // need to hardcode exactly how the types in the signTypedData are
        bytes32 typeHash = keccak256(abi.encodePacked('string Message'));
        bytes32 valueHash = keccak256(abi.encodePacked(message));
        return keccak256(abi.encodePacked(typeHash, valueHash)).recover(signature);
    }

    /**
     * @dev _setKV is used to set dataMap.
     * @param k the key for _datamap.
     * @param v the value for _datamap.
     */
    function _setKV(address k, StoreData memory v) private {
        _dataMap[k] = v;
        _metaData.registTotal = _metaData.registTotal + 1;
    }

    /**
     * @dev _getKV is used to get the value by the key.
     * @param k the key for _datamap.
     */
    function _getKV(address k) private view  returns (StoreData memory sd) {
        return _dataMap[k];
    }

    /**
     * @dev _set is used to set _metaData.
     * @param wallet the wallet address specified by the creator.
     * @param nftAddr the NFT address specified by the creator.
     * @param storeFee one-time storage fee for user registration.
     * @param feeRate fee deduction percentage set by the contract creator, demo: 500 / 10000, feeRate is 500.
     */
    function _set(address wallet, address nftAddr, uint256 storeFee, uint256 feeRate) private {
        _metaData = MetaData(wallet, nftAddr, 0, 0, 0);
        if (nftAddr == address(0)) {
            _metaData.storeFee = storeFee;
            _metaData.feeRate = feeRate;
        }
    }

    /**
     * @dev _get is used to load the meta information of the contract.
     */
    function _get() private view returns (MetaData memory md) {
        return _metaData;
    }
}